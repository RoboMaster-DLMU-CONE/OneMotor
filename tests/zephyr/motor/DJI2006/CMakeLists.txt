cmake_minimum_required(VERSION 3.20.0)
find_package(Zephyr REQUIRED HINTS $ENV{ZEPHYR_BASE})
project(onemotor_dji_motor_test)

target_sources(app PRIVATE src/main.cpp)

function(get_all_targets var)
    set(targets)
    get_all_targets_recursive(targets ${CMAKE_CURRENT_SOURCE_DIR})
    set(${var} ${targets} PARENT_SCOPE)
endfunction()

macro(get_all_targets_recursive targets dir)
    get_property(subdirs DIRECTORY ${dir} PROPERTY SUBDIRECTORIES)
    foreach(subdir ${subdirs})
        get_all_targets_recursive(${targets} ${subdir})
    endforeach()
    get_property(current_targets DIRECTORY ${dir} PROPERTY BUILDSYSTEM_TARGETS)
    list(APPEND ${targets} ${current_targets})
endmacro()

# 执行检查
get_all_targets(all_targets)
foreach(tgt ${all_targets})
    # 检查 INCLUDE_DIRECTORIES
    get_target_property(inc_dirs ${tgt} INCLUDE_DIRECTORIES)
    if(inc_dirs)
        foreach(dir ${inc_dirs})
            if("${dir}" MATCHES "^/usr/include")
                message(FATAL_ERROR "Found POISON header path in target: [${tgt}] -> ${dir}")
            endif()
        endforeach()
    endif()

    # 检查 INTERFACE_INCLUDE_DIRECTORIES
    get_target_property(iface_dirs ${tgt} INTERFACE_INCLUDE_DIRECTORIES)
    if(iface_dirs)
        foreach(dir ${iface_dirs})
            if("${dir}" MATCHES "^/usr/include")
                message(FATAL_ERROR "Found POISON header path in target (INTERFACE): [${tgt}] -> ${dir}")
            endif()
        endforeach()
    endif()
endforeach()
message(STATUS "No system include pollution found (yet).")